apply plugin: 'android'

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.0'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19
    }
    signingConfigs {
        release {
            storeFile file("../keystore/TechCampSG.jks")
            storePassword "****"
            keyAlias "release"
            keyPassword "****"
        }
    }


    buildTypes {
        release {
            signingConfig signingConfigs.release
            //runProguard false
            //proguardFiles getDefaultProgluardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile project(':pull-to-refresh-library')

    compile 'com.android.support:support-v4:+'
    compile 'com.android.support:appcompat-v7:19.0.+'
    compile 'com.google.android.gms:play-services:4.0.30'
    //compile 'com.github.chrisbanes.actionbarpulltorefresh:extra-abc:+'
    compile 'com.google.code.gson:gson:+'
    compile 'com.readystatesoftware.sqliteasset:sqliteassethelper:+'
    compile files('libs/android-async-http-1.4.4.jar')
    compile files('libs/picasso-2.1.1.jar')
}

task askForPasswords << {
    // Must create String because System.readPassword() returns char[]
    // (and assigning that below fails silently)
    def storePw = new String(System.console().readPassword("Keystore password: "))
    def keyPw  = new String(System.console().readPassword("Key password: "))

    android.signingConfigs.release.storePassword = storePw
    android.signingConfigs.release.keyPassword = keyPw
}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals("packageRelease")) {
        theTask.dependsOn "askForPasswords"
    }
}
